(in-package :cl-user)

(needs (:org.tfeb.dsm :compile t :use t))

(defmacro define-matching-macro (name &body clauses)
  (let ((<whole> (make-symbol "WHOLE"))
        (<junk> (make-symbol "JUNK")))
    (multiple-value-bind (doc the-clauses)
        (if (stringp (first clauses)) 
            (values (first clauses) (rest clauses))
          (values nil clauses))
      `(defmacro ,name (&whole ,<whole> &rest ,<junk>)
         ,@(if doc (list doc) '())
         (declare (ignore ,<junk>))
         (destructuring-match ,<whole> ,@the-clauses)))))

(define-matching-macro bind*
  ((_ () . forms)
   `(locally ,@forms))
  ((_ ((var &optional (val nil))) . forms)
   (:when (symbolp var))
   `((lambda (,var) ,@forms) ,val))
  ((_ (var) . forms)
   (:when (symbolp var))
   `((lambda (,var) ,@forms) nil))
  ((_ ((var &optional (val nil)) . more) . forms)
   (:when (symbolp var))
   `((lambda (,var) (bind* ,more ,@forms)) ,val))
  ((_ (var . more) . forms)
   (:when (symbolp var))
   `((lambda (,var) (bind* ,more ,@forms)) nil))
  (otherwise
   (error "what even is this?")))
